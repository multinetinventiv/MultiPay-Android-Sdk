apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = deps.maven.PUBLISH_GROUP_ID
version = deps.maven.PUBLISH_VERSION

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["sonatypeStagingProfileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(secretPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
} else {
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('OSSRH_USERNAME')
    ext["ossrhPassword"] = System.getenv('OSSRH_PASSWORD')
    ext["sonatypeStagingProfileId"] = System.getenv('SONATYPE_STAGING_PROFILE_ID')
}

publishing {
    publications {
        release(MavenPublication) {
            groupId deps.maven.PUBLISH_GROUP_ID
            artifactId deps.maven.PUBLISH_ARTIFACT_ID
            version deps.maven.PUBLISH_VERSION

            // Two artifacts, the `aar` (or `jar`) and the sources
            if (project.plugins.findPlugin("com.android.library")) {
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            } else {
                artifact("$buildDir/libs/${project.getName()}-${version}.jar")
            }
            artifact androidSourcesJar

            // Mostly self-explanatory metadata
            pom {
                name = deps.maven.PUBLISH_ARTIFACT_ID
                description = 'MultiPay official Android SDK'
                url = 'https://github.com/multinetinventiv/MultiPay-Android-Sdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/multinetinventiv/MultiPay-Android-Sdk/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'erimkorkmaz'
                        name = 'Mehmet Erim Korkmaz'
                        email = 'mehmeterim.korkmaz@inventiv.com.tr'
                    }
                    developer {
                        id = 'NaCI'
                        name = 'Naci Ozyildirim'
                        email = 'naci.ozyildirim@inventiv.com.tr'
                    }
                    developer {
                        id = 'uLbyy'
                        name = 'Huseyin Aslan'
                        email = 'huseyin.aslan@inventiv.com.tr'
                    }
                }
                organization {
                    name = 'Inventiv'
                    url = 'https://inventiv.com.tr'
                }
                scm {
                    connection = 'scm:git:github.com/multinetinventiv/MultiPay-Android-Sdk.git'
                    developerConnection = 'scm:git:ssh://github.com/multinetinventiv/MultiPay-Android-Sdk.git'
                    url = 'https://github.com/multinetinventiv/MultiPay-Android-Sdk/tree/main'
                }
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Library Dependencies
                    def map = [
                            [groupId: "org.jetbrains.kotlin", artifactId: "kotlin-android-extensions-runtime", version: "1.4.20"],
                            [groupId: "org.jetbrains.kotlin", artifactId: "kotlin-stdlib-jdk7", version: "1.4.0"],
                            [groupId: "androidx.core", artifactId: "core-ktx", version: "1.3.2"],
                            [groupId: "androidx.appcompat", artifactId: "appcompat", version: "1.2.0"],
                            [groupId: "androidx.constraintlayout", artifactId: "constraintlayout", version: "1.1.3"],
                            [groupId: "com.google.android.material", artifactId: "material", version: "1.2.1"],
                            [groupId: "com.android.volley", artifactId: "volley", version: "1.1.1"],
                            [groupId: "com.google.code.gson", artifactId: "gson", version: "2.8.6"],
                            [groupId: "androidx.lifecycle", artifactId: "lifecycle-livedata-ktx", version: "2.2.0"],
                            [groupId: "androidx.lifecycle", artifactId: "lifecycle-viewmodel-ktx", version: "2.2.0"],
                            [groupId: "androidx.databinding", artifactId: "viewbinding", version: "4.1.1"]
                    ]

                    map.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.groupId)
                        dependencyNode.appendNode('artifactId', it.artifactId)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"

            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}